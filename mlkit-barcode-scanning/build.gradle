plugins {
    id 'com.android.library'
    id 'maven-publish'
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = 'mlkit-barcode-scanning'
                groupId = "com.github.hachimann"
                version = '0.0.1'
                pom {
                    name = 'Mlkit Barcode Scanning'
                    description = 'Barcode scanner library for Android, based on ML Kit\'s barcode scanning API'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/Hachimann/mlkit-barcode-scanning/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'hachimann'
                            name = 'Shandor Hachimann'
                        }
                    }
                }
            }
        }
    }
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.mlkit:barcode-scanning:17.0.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation 'org.jetbrains:annotations:15.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}